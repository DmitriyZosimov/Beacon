plugins {
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagmentVersion"
    id 'org.asciidoctor.convert' version "$asciidoctorConvertVersion"
    id 'maven-publish'
    id 'jacoco'
    id 'org.flywaydb.flyway' version "$flywayVersion"
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {

    implementation project (':model')
    implementation project (':security')

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jersey'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    compileOnly "org.flywaydb:flyway-core:$flywayVersion"

    compileOnly lombok.lombok
    annotationProcessor lombok.lombok
    runtimeOnly postgresql.postgresql

    testRuntimeOnly h2.h2
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

bootJar {
    mainClass = 'com.beacon.catalog.CatalogApplication'
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}

project.afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                artifact bootJar
            }
        }
    }
}

jacoco {
    toolVersion = "$jacocoVersion"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled true
        csv.destination file("${buildDir}/reports/jacoco/jacocoTestReport-catalog.csv")
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/beacon/catalog/CatalogApplication.class"
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }
    }
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/catalog'
    user = 'flyway'
    password = 'flyway'
    locations = ['classpath:db/migration']
}